import java.text.SimpleDateFormat

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

android {
    namespace 'org.thecosmicfrog.luasataglance'

    compileSdk 34

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = '17'
    }

    defaultConfig {
        applicationId "org.thecosmicfrog.luasataglance"
        minSdk 29
        targetSdk 34
        versionCode generateVersionCode()
        versionName generateVersionName()
    }

    buildFeatures {
        viewBinding true
    }

    buildTypes {
        debug{
            resValue "string", "version_name", generateVersionName()
            resValue "string", "release_date", generateReleaseDate()
        }
        release {
            resValue "string", "version_name", generateVersionName()
            resValue "string", "release_date", generateReleaseDate()

            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    productFlavors {
    }
}

dependencies {
    api fileTree(include: ['*.jar'], dir: 'libs')
    api 'androidx.appcompat:appcompat:1.6.1'
    api 'androidx.cardview:cardview:1.0.0'
    api 'com.google.android.material:material:1.9.0'
    api 'com.squareup.retrofit:retrofit:1.9.0'
    api 'com.squareup.okhttp:okhttp:2.7.5'
    api 'androidx.legacy:legacy-support-v4:1.0.0'
    api 'com.google.android.gms:play-services-maps:18.1.0'
    implementation "androidx.preference:preference-ktx:1.2.1"
    implementation 'com.google.android.gms:play-services-location:21.0.1'
    implementation 'pub.devrel:easypermissions:3.0.0'
    implementation "androidx.core:core-ktx:1.10.1"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.12'
}

private Integer generateVersionCode() {
    project.versionCode
}

private String generateVersionName() {
    String.format("0.%s", Integer.toString(generateVersionCode()))
}

private static String generateReleaseDate() {
    Date currentDate = new Date()
    SimpleDateFormat dateFormat = new SimpleDateFormat("dd MMMM yyyy", Locale.getDefault())
    String currentDateFormatted = dateFormat.format(currentDate)

    currentDateFormatted
}
